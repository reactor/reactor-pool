/*
 * Copyright (c) 2018-2022 VMware Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

javadoc {
	dependsOn jar
	group = "documentation"
	description = "Generates Javadoc API documentation."

	afterEvaluate {
		title = "${shortName} ${version}"
		options.header = "$shortName"
	}

	if (JavaVersion.current().isJava8Compatible()) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
	options.addStringOption('charSet', 'UTF-8')
	options.tags = [ "apiNote:a:API Note:"]
	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.stylesheetFile = file("$rootDir/docs/api/stylesheet.css")

	if (project.name == 'reactor-pool') {
		options.links([
				"https://projectreactor.io/docs/core/${libs.versions.reactorCore.get()}/api/",
				jdkJavadoc,
				//forcing RS javadoc on version 1.0.3 as it appears 1.0.4 cannot be reached/consumed
				"https://www.reactive-streams.org/reactive-streams-1.0.3-javadoc/"
		] as String[])
	}
	else {
		options.links([
				"https://projectreactor.io/docs/core/${libs.versions.reactorCore.get()}/api/",
				jdkJavadoc,
				//forcing RS javadoc on version 1.0.3 as it appears 1.0.4 cannot be reached/consumed
				"https://www.reactive-streams.org/reactive-streams-1.0.3-javadoc/",
				//TODO currently java 8 cannot link to Micrometer javadocs deployed to javadoc.io
				"https://projectreactor.io/docs/pool/${libs.versions.baseline.pool.api.get()}/api/"
		] as String[])
	}

	maxMemory = "1024m"
	destinationDir = new File(project.buildDir, "docs/javadoc")
	source project.sourceSets.main.allJava
	doFirst {
		classpath = files(project.sourceSets.main.compileClasspath)
	}
}

