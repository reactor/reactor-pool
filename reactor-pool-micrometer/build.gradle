/*
 * Copyright (c) 2022-2025 VMware Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'java-library'

description = 'Micrometer metrics support for Reactor Pool'
version = "$reactorPoolMicrometerVersion"

ext {
    shortName = "Reactor Pool Micrometer"
    osgiVersion = osgiVersion(version.toString())
    bndOptions = [
      "Export-Package": [
        "!*internal*",
        "reactor.pool.introspection.micrometer.*;version=$osgiVersion"
      ].join(","),
      "Import-Package": [
        "!javax.annotation",
        "*"
      ].join(","),
      "Bundle-Name" : "reactor-pool-micrometer",
      "Bundle-SymbolicName" : "io.projectreactor.addons.reactor-pool-micrometer",
      "Bundle-Version" : "$osgiVersion"
    ]
}

dependencies {
    // Reactor
    api libs.reactor.core
    api project(":reactor-pool")

    // Micrometer
    api platform(libs.micrometer.bom)
    api libs.micrometer.core
    // Micrometer for tests
    testImplementation platform(libs.micrometer.bom)
    testImplementation libs.micrometer.test

    // JSR-305 annotations
    compileOnly libs.jsr305

    // Testing
    testImplementation libs.assertj
    testRuntimeOnly libs.slf4j.jcl
    testRuntimeOnly libs.logback
    testImplementation libs.reactor.test
    testImplementation platform(libs.junit.bom)
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

//TODO Add japicmp once GA reached

jar {
    manifest {
        attributes("Created-By": "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
          "Implementation-Title": project.name,
          "Implementation-Version": project.version,
          "Automatic-Module-Name": "reactor.pool.micrometer")
    }
    bnd(bndOptions)
}

check.dependsOn jacocoTestReport